/* 秀丸エディタからEverythingを使用するマクロ。

-----------------------------------------
概要
-----------------------------------------
秀丸エディタからファイル検索ソフトの「Everything」を使用してファイルを検索します。
秀丸エディタの中から一瞬たりとも抜けたくない方向けのマクロです。


「検索文字列の入力方法・検索結果の表示方法」の２点についてカスタマイズが可能です。

	＊検索結果の表示方法
	  アウトプット枠
	  新規ファイルへ出力
	  カーソル位置へ挿入

	＊検索文字列の指定
	  入力ボックス
	  カーソルのある行の文字

カスタマイズについては、本マクロ中の「設定（オプション）」のコメントを参照して下さい。



----------------------------------------
インストール方法
----------------------------------------
・始めに、Everything をインストールします。
・次に、es.zip をダウンロードして zipファイル中の es.exe を適切なフォルダへコピーして下さい。
    (例)C:\Program Files\Everything\es.exe
・本マクロ中の$g_exe変数（es.exeへのパス）を必要に応じて書き換えて下さい。

（注意）
・予め、Everything が正常動作することを確認して下さい。



----------------------------------------
動作確認を行った環境
----------------------------------------
秀丸エディタ v8.40 beta19 32bit
Everything-1.3.3.658b.x64.exe
Windows7 64bit



----------------------------------------
リンク
----------------------------------------
EverythingのWEBページ
http://www.voidtools.com/

Everything日本語ヘルプ
https://sites.google.com/site/everythingjphelp/



----------------------------------------
更新履歴
----------------------------------------
2018/10/08	v2.0.0
2014/09/15	v1.0.1 コメントの編集
2014/05/10	v1.0.0 公開

----------------------------------------
連絡先
----------------------------------------
http://d.hatena.ne.jp/ohtorii/
https://twitter.com/ohtorii
*/



/*****************************************************************************
        設定（必須）
*****************************************************************************/
/* es.exe へのパス */
$g_exe = "C:\\Program Files\\Everything\\es.exe";

/*es.exe のオプション
詳細は以下コマンドを実行して下さい。
> es.exe --help
*/
$g_option = "-p -s -n 200";





/*****************************************************************************
		メイン処理
*****************************************************************************/
//バージョン番号
$g_version = "version 2.0.0(2018/10/08)";
$dengaku_dll = hidemarudir + "\\DengakuDLL.dll";
//空白文字の定義
$g_space="\r\n\t 　";
//ダイアログの設定を保存するファイル名
$g_ini_filename=currentmacrofilename+".config.ini";
$g_ini_dialog_section="dialog";
$g_ini_dialog_key="input";

//
//ダイアログ関係
//
//ダイアログに入力された検索パターン
$g_search_pattern="";
//ダイアログの通知メッセージ
$g_notify_close="10";



call Main;
endmacro;

Main:
	setcompatiblemode 0x20000;
	call CheckEnvironment;
	if(! ##return){
		return false;
	}
	loaddll $dengaku_dll;
	if (!result) {
		message "田楽DLLのロードに失敗しました\n"
		      + "DengakuDLL.dllが秀丸のディレクトリに存在するか確認してください";
		return false;
	}
	call Start;
	##result=##return;
	freedll;
	return ##result;


Start:
	call CreateDialog;
	if(! ##return){
		return false;
	}
	
	//debuginfo 1;
	
	//文字入力中に適度な間隔でファイル内容を更新する。
	##interval			= 300;
	##prev_tick 		= 0;
	$$prev_search_pattern="";
	while (1) {
		##current_tick = tickcount;
		if(##interval < (##current_tick - ##prev_tick)){
			$g_search_pattern = dllfuncstr("GETCTRLSTRING", "CTagsDlg@Param");
			call TrimString $g_search_pattern;
			$g_search_pattern=$$return;
			if($g_search_pattern != $$prev_search_pattern){
				if($g_search_pattern == ""){
					//後回し
					//call ClearText;
				}else{
					//call RunEverything  $g_search_pattern;
					call SearchWordsToRegex $g_search_pattern;
					debuginfo $$return;
				}
				$$prev_search_pattern=$g_search_pattern;
			}
			##prev_tick = ##current_tick;
		}
		
		$$item = dllfuncstr("WAITCTRLNOTIFY",60);
		if($$item=="0"){
			break;
		}else if($$item=="0"){
			break;
		}else if ($$item == $g_notify_close) {
			break;
		}
	}
	##n = dllfunc("ENDDIALOG");
	
	//#writeinistr $g_ini_filename,$g_ini_dialog_section,$g_ini_dialog_key,$$current_input;
	return true;

CreateDialog:
	$g_search_pattern = getinistr($g_ini_filename,$g_ini_dialog_section,$g_ini_dialog_key);
	if (dllfunc("NEWDIALOG", "Everythingでファイルを検索するマクロ " + $g_version, 128) == 0 ||
		dllfunc("NEWCONTROL", "text", "", "検索(S)") == 0 ||
		dllfunc("SETCTRLWIDTH", "", 8) == 0 ||
		dllfunc("NEWCONTROL", "edit", "CTagsDlg@Param", $g_search_pattern) == 0 ||
		dllfunc("NEWCONTROL","defbutton","","Close") == 0 ||
		dllfunc("SETCTRLNOTIFY", "", $g_notify_close) == 0)
	{
		message "ダイアログの作成に失敗しました\n";
		return false;
	}
	// ダイアログの表示
	##n = dllfunc("SHOWDIALOG", 0, 0);
	return true;

InputFromText:
	if(selecting){
		$$search_words = gettext2(seltopcolumn,seltoplineno,selendcolumn,selendlineno,1);
	}else{
		##old_x=x;
		##old_y=y;
		selectline;
		$$search_words = gettext2(seltopcolumn,seltoplineno,selendcolumn,selendlineno,0);
		moveto  ##old_x, ##old_y;
	}
	return $$search_words;


CheckEnvironment:
	if(! existfile($g_exe)){
		message "Everythingのコマンドライン実行ファイルが見つかりません。\n" +
				"\n"+
				"見つからないファイル\n"+
				$g_exe;

		return false;
	}
	return true;

RunEverything:
	$$search_words=$$1;
	$$arg_exe = "\"" + $g_exe + "\"";
	$$option = $g_option + " -p";
	$$run_str = $$arg_exe + " " + $$option + " " + $$search_words;
	if(true){
		disabledraw;
		selectall;
		backspace;
		enabledraw;
		runex $$run_str,
				0,						//sync	  0:async 1:sync
				0, "",					//stdin   0:none 1:auto 2:file 3:(reserve) 4:all 5:select
				5, "",		//stdout  0:none 1:auto 2:file 3:add file  4:new 5:insert 6:replace
				5, "",		//stderr  0:none 1:=out 2:file 3:add file  4:new 5:insert 6:replace
				1, "",					//folder  0:none 1:current 2:specify 3:(reserve) 4:exe's folder
				2,						//show	  0:auto 1:show 2:hide
				0,						//nodraw  0:draw 1:no draw
				0						//unicode 0:ansi 2:unicode
				;
		call HilightWrold $$search_words;
	}else{
		runex $$run_str,
				1,						//sync	  0:async 1:sync
				0, "",					//stdin   0:none 1:auto 2:file 3:(reserve) 4:all 5:select
				7, "",		//stdout  0:none 1:auto 2:file 3:add file  4:new 5:insert 6:replace
				7, "",		//stderr  0:none 1:=out 2:file 3:add file  4:new 5:insert 6:replace
				1, "",					//folder  0:none 1:current 2:specify 3:(reserve) 4:exe's folder
				2,						//show	  0:auto 1:show 2:hide
				0,						//nodraw  0:draw 1:no draw
				0						//unicode 0:ansi 2:unicode
				;
	}
	return result;


HilightWrold:
	$$search_words=$$1;
	call SearchWordsToRegex $$search_words;
	$$regex=$$return;
	setsearch $$regex,0x00000800;
	hilightfound 1;
	return;


SearchWordsToRegex:
	/*空白区切りの文字列を正規表現に変換する。
	[in]	"hoge 123 .txt"
	[out]	"hoge|123|\.txt"
	*/
	$$word_list=$$1;
	$$escaped = dllfuncstr("HMJRE_REPLACE_REGULAR", "[+.|?*()\\[\\]\\\\$\"-]", $$word_list, 0, "\\\\\\0", 1);
	$$regex = dllfuncstr("ReplaceRegular", "[" + $g_space + "]+", $$escaped, 0, "|", 2);
	return $$regex;

TrimString:
	while(1){
	  ##word=strlen($$1);
	  //文字列の先頭に空白文字がある場合、空白文字を削除
	  if(strstr($g_space,leftstr($$1,2))!=-1)$$1=rightstr($$1,##word-2);
	  else if(strstr($g_space,rightstr($$1,2))!=-1)$$1=leftstr($$1,##word-2);
	  //ここまで全角文字の対処
	  else if(strstr($g_space,leftstr($$1,1))!=-1)$$1=rightstr($$1,##word-1);
	  else if(strstr($g_space,rightstr($$1,1))!=-1)$$1=leftstr($$1,##word-1);
	  else break;
	}
	return $$1;