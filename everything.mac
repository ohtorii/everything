/* 秀丸エディタからEverythingを使用するマクロ。

超高速なファイル検索ソフトである「Everything」を使用してファイルを検索します。

検索結果は「アウトプット枠／新規ファイルへ出力／カーソル位置へ挿入」から選択可能
です、好みに合わせて本マクロをカスタマイズして下さい。
カスタマイズについては後述を参照して下さい。


（前準備）
・予め、Everythingをインストールし正常動作することを確認して下さい。


（インストール方法）
・es.exeをダウンロードして適切なフォルダへコピーして下さい。
    (例)C:\Program Files\Everything\es.exe
・本マクロ中の$g_exe変数（es.exeへのパス）を必要に応じて書き換えて下さい。

（）


（カスタマイズ）


（動作確認を行った環境）
秀丸エディタ v8.40 beta10 32bit
Everything-1.3.3.658b.x64.exe
Windows7 64bit


（リンク）
EverythingのWEBページ
http://www.voidtools.com/

Everything日本語ヘルプ
https://sites.google.com/site/everythingjphelp/


（更新履歴）
2014/05/10	作成
*/

/*****************************************************************************
        設定（必須）
*****************************************************************************/
/* es.exe へのパス
*/
$g_exe = "C:\\Program Files\\Everything\\es.exe";

/*es.exe のオプション
詳細はコマンドを実行して下さい。
> es.exe --help 
*/
$g_option = "-p -s -n 200";


/*****************************************************************************
        設定（オプション）
*****************************************************************************/
/*動作モード
0 - 選択している文字を使用する。（選択が無ければカーソルのある行の文字を使用する。）
1 - 入力BOX
*/
#g_input_mode=0;

/*出力モード
0 - アウトプット枠
1 - 新規ファイルへ出力
2 - カーソル位置へ挿入
*/
#g_output_mode = 0;










/*****************************************************************************
		処理
*****************************************************************************/
call Main;
endmacro;

Main:
	call CheckEnvironment;
	if(! ##return){
		return false;
	}
	call GetSearchText;
	if($$return==""){
		return ;
	}
	$$search_words=$$return;
	call ConvertOutputMode;
	##output_mode = ##return;
	
	call RunEverything ##output_mode, $$search_words;
	return ##return;
	
InputFromText:
	if(selecting){
		$$search_words = gettext2(seltopcolumn,seltoplineno,selendcolumn,selendlineno,1);
	}else{
		##old_x=x;
		##old_y=y;
		selectline;
		$$search_words = gettext2(seltopcolumn,seltoplineno,selendcolumn,selendlineno,1);
		moveto  ##old_x, ##old_y;
	}
	return $$search_words;

InputFromBox:
	return input("検索するファイル名を入力して下さい。");

ConvertOutputMode:
	if(#g_output_mode==0){
		return 7;
	}
	if(#g_output_mode==1){
		return 4;
	}
	if(#g_output_mode==2){
		return 5;
	}
	return 0;

CheckEnvironment:
	if(! existfile($g_exe)){
		message "Everythingのコマンドライン実行ファイルが見つかりません。\n" +
				"\n"+
				"見つからないファイル\n"+
				$g_exe;
				
		return false;
	}
	return true;

GetSearchText:
	if(#g_input_mode==0){
		call InputFromText;
		return $$return;
	}else if(#g_input_mode==1){
		call InputFromBox;
		return $$return;
	}else{
		//pass
	}
	return "";

RunEverything:
	##output_mode=##1;
	$$search_words=$$2;
	$$arg_exe = "\"" + $g_exe + "\"";
	$$option = "-p";
	$$run_str = $$arg_exe + " " + $$option + " " + $$search_words;
	//message $$run_str;
	runex $$run_str,
			0, 			//sync	  0:async 1:sync
			0, "", 		//stdin   0:none 1:auto 2:file 3:(reserve) 4:all 5:select
			##output_mode, "", 		//stdout  0:none 1:auto 2:file 3:add file  4:new 5:insert 6:replace
			##output_mode, "", 		//stderr  0:none 1:=out 2:file 3:add file  4:new 5:insert 6:replace
			1, "", 		//folder  0:none 1:current 2:specify 3:(reserve) 4:exe's folder
			2, 			//show	  0:auto 1:show 2:hide
			0, 			//nodraw  0:draw 1:no draw
			0 			//unicode 0:ansi 2:unicode
			;
	return result;