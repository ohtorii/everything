/*プロジェクトフォルダを探す

（利用例）
execmacro "search_project_folder.mac" "c:\\my_project\\src\system\\heap";

（マクロの返値）
プロジェクトルートのフォルダを見つけるとその文字列を返します、
見つからない場合は空文字を返します。
*/

$g_markers[0] = "";
#g_markers_len=0;

$g_ini_config_filename=currentmacrofilename+".config.ini";
#g_dll 	= 0;


debuginfo 0;
call main;
endmacro $$return;


main:
	if(! existfile($g_ini_config_filename)){
		message "iniファイルが見つからない\n"+$g_ini_config_filename;
		return false;
	}
	#g_dll=loaddll(hidemarudir+"\\ht_tools.dll");
	if(#g_dll==0){
		message "ht_tools.dll のロードに失敗しました\n" +
				"ht_tools.dll が秀丸エディタのディレクトリに存在するか確認してください";
		return false;
	}
	
	$$current_folder=getarg(0);
	call start;
	$$ret=$$return;

	freedll(#g_dll);
	#g_dll=0;
	return $$ret;


start:
	call load_ini;
	if(! ##return){
		return "";
	}
	call findroot $$current_folder;
	$$project_folder=$$return;

	debuginfo "project_folder=" + $$project_folder;
	return $$project_folder;


findroot:
	$$current_folder=$$1;
	debuginfo "$$current_folder="+$$current_folder;

	##i=0;
	while(##i < #g_markers_len){
		$$sub_folder=$$current_folder+"\\"+$g_markers[##i];
		if(existfile($$sub_folder,1) & 0x00000010){
			return $$current_folder;
		}
		##i = ##i + 1;
	}
	if(strlen($$current_folder) <= 3){
		//"c:\" のパターン
		return "";
	}

	call getparent $$current_folder;
	$$parent=$$return;
	call findroot $$parent;
	return $$return;


getparent:
	$$parent = dllfuncstr(#g_dll, "GetParentFolder", $$1);
	return $$parent;

load_ini:
	##i=0;
	while(##i<100){
		$$item = getinistr($g_ini_config_filename,"project_folder","_"+str(##i));
		debuginfo "$$item="+$$item;
		if($$item==""){
			return true;
		}
		$g_markers[##i]=$$item;
		#g_markers_len = #g_markers_len + 1;
		
		##i = ##i + 1;
	}
	return false;